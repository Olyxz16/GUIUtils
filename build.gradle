plugins {
    id 'java'
    id 'maven-publish'
}

group = 'org.olyxz16'
version = '1.0-SNAPSHOT'
description = 'GUIUtils'

repositories {
    mavenCentral()
    maven {
        url = uri("https://hub.spigotmc.org/nexus/content/repositories/snapshots/")
    }
}

dependencies {
    compileOnly("org.spigotmc:spigot-api:1.20-R0.1-SNAPSHOT")
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

publishing {
    repositories {
        maven {
            url = uri("https://maven.pkg.github.com/Olyxz16/GUIUtils")
            credentials {
                username = System.getProperty('GITHUB_USERNAME')
                password = System.getProperty('GITHUB_TOKEN')
            }
        }
    }
    publications {
        maven(MavenPublication) {
            groupId group
            version version
            artifactId 'guiutils'
            
            from components.java
        }
    }
}